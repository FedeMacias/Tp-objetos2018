import Hechiceria.*

describe "Tests de lucha y hechiceria avanzada" {

	const armadura = new Armadura()

	fixture {
		rolando.agregarVariosArtefactos([ espadaDelDestino, collarDivino, mascaraOscura, espejoFantastico, armadura ])
		collarDivino.cantidadDePerlas(5)
		rolando.hechizoFavorito(espectroMalefico)
	}

	test "1)rolando esta cargado" {
		assert.that(rolando.estaCargado())
	}

	test "2)Sin armadura rolando NO esta cargado" {
		rolando.quitarArtefacto(armadura)
		assert.that(rolando.estaCargado().negate())
	}

	test "3)La habilidad de lucha de rolando es de 20 puntos" {
		assert.equals(rolando.habilidadDeLucha(), 20)
	}

	test "4)Con una bendicion en la armadura, la habilidad de lucha de la armadura es de 58 puntos" {
		armadura.refuerzo(bendicion)
		assert.equals(armadura.unidadesLucha(), 58)
	}

	test "5)La habilidad de lucha de una armadura con refuerzo de cota de malla es de 3 puntos" {
		armadura.refuerzo(cotaDeMalla)
		assert.equals(armadura.unidadesLucha(), 3)
	}

	test "6)La habilidad de lucha de la armadura reforzada por el hechizo basico es de 12 puntos" {
		hechizo.hechizoDeRefuerzo(hechizoBasico)
		armadura.refuerzo(hechizo)
		assert.equals(armadura.unidadesLucha(), 12)
	}

	test "7)La habilidad de la armadura reforzada por espectro malefico es de 19 puntos" {
		hechizo.hechizoDeRefuerzo(espectroMalefico)
		armadura.refuerzo(hechizo)
		assert.equals(armadura.unidadesLucha(), 19)
	}

	test "8)Solo con el espejo, la habilidad de lucha de rolando es de 1 punto" {
		rolando.quitarTodoArtefacto()
		rolando.agregarArtefacto(espejoFantastico)
		assert.equals(rolando.habilidadDeLucha(), 1)
	}

	test "9)Con un libro de hechizos que contiene espectro malefico y hechizo basico, el nivel de hechiceria de rolando es de 56 puntos" {
		libroDeHechizos.agregarVariosHechizos([ espectroMalefico, hechizoBasico ])
		rolando.hechizoFavorito(libroDeHechizos)
		assert.equals(rolando.nivelHechiceria(), 56)
	}
	
}

